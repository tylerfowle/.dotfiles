# zmodload zsh/zprof # top of your .zshrc file

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles
export PATH="/usr/local/opt/ruby/bin:$PATH"
export PATH="/usr/local/lib/ruby/gems/2.6.0/bin:$PATH"

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
[ -f ~/.q.plugin.zsh ] && source ~/.q.plugin.zsh

eval "$(pyenv init -)"

# add zsh syntax highlighting
if [[ -a ~/Repos/zsh-syntax-highlighting/zsh-syntax-highlighting.plugin.zsh ]]
then
  source ~/Repos/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

# zprof # bottom of .zshrc
